// Gora DAO Smart Contract
// Version 1
// Website: https://www.gora.io/
// Creators: @emg110.algo
// Github: https://github.com/GoraNetwork/gora-dao-smartcontracts
#pragma version 8
//Check TXN first
callsub sub_check_txn

// Check creation TXN
txn ApplicationID

bz branch_create

// Checking onCompletion type for update application call
txn OnCompletion
int UpdateApplication
==
bnz branch_update

// Checking onCompletion type for delete application call
txn OnCompletion
int DeleteApplication
==
bnz branch_delete

// Checking onCompletion type for close out application call
txn OnCompletion
int CloseOut
==
bnz branch_error

// Checking onCompletion type for optin application call
txn OnCompletion
int OptIn
==
bnz branch_error

// Checks the number of application args , if 0 then no method it is
txn NumAppArgs

int 0
==
bz branch_method_router
b branch_error

check_owner_auth:

	txn Sender
	global CreatorAddress
	==
	assert
	retsub

branch_create:

	global CurrentApplicationID
	store 1

	byte "GoraDAO contract created!"
	store 2

	load 1
	itob
	log

	load 2
	log
	
	byte "gora_dao_manager_address"
	txn Sender
	app_global_put
	byte "total_proposals_count"
	int 0
	app_global_put

	byte "ready_proposals_count"
	int 0
	app_global_put

	byte "active_proposals_count"
	int 0
	app_global_put

	byte "total_votes_count"
	int 0
	app_global_put

	int 1
	return

branch_delete:

	callsub check_owner_auth

	int 1
	return

branch_update:

	callsub check_owner_auth

	global CurrentApplicationID
	store 1

	byte "GoraDAO contract updated: "
	store 2

	load 2
	load 1
	itob
	concat
	log
	byte "gora_dao_manager_address"
	txn Sender
	app_global_put

	byte "total_proposals_count"
	int 0
	app_global_put

	byte "ready_proposals_count"
	int 0
	app_global_put

	byte "active_proposals_count"
	int 0
	app_global_put

	byte "total_votes_count"
	int 0
	app_global_put


	int 1
	return



branch_method_router:

	method "config_dao(pay,asset,uint64,address)string"
	txn ApplicationArgs 0
	==
	bnz method_config_dao

	method "write_source_box(byte[],byte[])string"
	txn ApplicationArgs 0
	==
	bnz method_write_source_box


	method "create_proposal(pay,address,asset,uint64,string,string)uint64"
	txn ApplicationArgs 0
	==
	bnz method_proposal_create

	method "update_proposal(pay,application,uint64,address,uint64,string,string)uint64"
	txn ApplicationArgs 0
	==
	bnz method_proposal_update

	method "config_proposal(pay,account,application,asset,uint64)uint64"
	txn ApplicationArgs 0
	==
	bnz method_proposal_config

	b branch_error

sub_check_txn:

	// Checks RekeyTo address transaction field to be empty (ZeroAddress)
	txn RekeyTo

	global ZeroAddress
	==
	assert

	// Checks CloseRemainderTo address transaction field to be empty (ZeroAddress)
	txn CloseRemainderTo

	global ZeroAddress
	==
	assert

	// Checks AssetCloseTo address transaction field to be empty (ZeroAddress)
	txn AssetCloseTo

	global ZeroAddress
	==
	assert

	// Check transaction type to be application call
	txn TypeEnum

	int appl
	==
	assert

	// Checking transaction fee
	txn Fee

	global MinTxnFee
	>=
	assert
	retsub

sub_check_owner_auth:

	txn Sender
	global CreatorAddress
	==
	assert
	retsub
// Handles configuration of GoraDAO
method_config_dao:

	txn ApplicationArgs 1
	btoi
	store 1
	byte "gora_dao_asset_id"
	load 1
	app_global_put
	load 1
	itob
	log

	txn ApplicationArgs 2
	store 2
	byte "gora_dao_manager_address"
	load 2
	app_global_put

	load 2
	log


	byte 0x151f7c75
	byte "dao has configured!"
	concat
	b branch_log_return

method_write_source_box:

	callsub sub_check_owner_auth

	byte "proposal_app"
	store 1

	byte "proposal_clr"
	store 2

	load 1
	box_get
	bz branch_write_box_app

	load 1
	box_del
	pop
	load 2
	box_del
	pop
branch_write_box_app:

	load 1
	txn ApplicationArgs 1
	box_put
	load 2
	txn ApplicationArgs 2
	box_put
	byte 0x151f7c75
	byte "GoraDAO Teal Source Codes Updated!"
	concat
	b branch_log_return

// Handles ABI method call for contract to contract creation of a Proposal contract
method_proposal_create:

  	// Checking Transaction group
    global GroupSize
    int 2
    ==
    assert
    // Checking the pay txn at index 0 of gtxn
    gtxn 0 RekeyTo
    global ZeroAddress
    ==
    assert
    gtxn 0 CloseRemainderTo
    global ZeroAddress
    ==
    assert
    gtxn 0 AssetCloseTo
    global ZeroAddress
    ==
    assert
    gtxn 0 TypeEnum
    int pay
    ==
    assert
    gtxn 0 Fee
    global MinTxnFee
    >=
    assert
    gtxn 0 Amount
    global MinTxnFee
    >=
    assert
    // Checking if the receiver of pay txn is the same as the current application address
    txn GroupIndex
    int 1
    -
    dup
    gtxns Receiver
    global CurrentApplicationAddress
    ==
    assert

    gtxns Amount
    global MinTxnFee
    int 2
    *
    >=
    assert

    // Process Transaction args

    // Proposal manager address
    txn ApplicationArgs 1
    store 1

    // Proposal asset id
    txn ApplicationArgs 2
    btoi
    store 2

    // Proposal title
    txn ApplicationArgs 3
    store 3

    // Proposal description
    txn ApplicationArgs 4
    store 4

    // Inner transaction to create the Proposal smart contract
    itxn_begin
    // itxn Type to "appl"
    int appl
    itxn_field TypeEnum
    // itxn OnCompletion
    int NoOp
    itxn_field OnCompletion

    // itxn 
    int 7
    itxn_field GlobalNumByteSlice
    int 7
    itxn_field GlobalNumUint
    int 0
    itxn_field LocalNumUint
    int 0
    itxn_field LocalNumByteSlice
    txn Sender
    itxn_field Note
    method "create_proposal(application,uint64,uint64,address,address,string,string)address"
    itxn_field ApplicationArgs
    byte "proposal_app"
    box_get
    bz branch_error
    dup
    len
    int 2
    swap
    substring3
    itxn_field ApprovalProgram
    
    byte "proposal_clr"
    box_get
    bz branch_error
    dup
    len
    int 2
    swap
    substring3
    itxn_field ClearStateProgram
    global CurrentApplicationID
    itob
    itxn_field ApplicationArgs
    byte "gora_dao_asset_id"
    app_global_get
	itob
    itxn_field ApplicationArgs
    load 2
    itob
    itxn_field ApplicationArgs
    load 1
    itxn_field ApplicationArgs
    byte "gora_dao_manager_address"
	app_global_get
    itxn_field ApplicationArgs
    load 3
    itxn_field ApplicationArgs
    load 4
    itxn_field ApplicationArgs
    itxn_submit
    itxn TxID
    log
	itxn LastLog
	extract 4 0
    store 10
    load 10
    dup
    log
    global ZeroAddress
    !=
    load 10 
    len
    int 32
    ==
    &&
    assert



    itxn CreatedApplicationID
    store 9
    byte "total_proposals_count"
    dup
    app_global_get
    int 1
    +
    app_global_put

 	itxn_begin
	global CurrentApplicationAddress
	itxn_field Sender
	int pay
	itxn_field TypeEnum
	load 10
	itxn_field Receiver
	int 300000
	itxn_field Amount
	itxn_submit
	itxn TxID
    log

    itxn_begin
    method "optin_proposal_asset(asset,uint64)uint64"
    itxn_field ApplicationArgs
    int appl
	itxn_field TypeEnum
    int NoOp
    itxn_field OnCompletion
    load 9
	itxn_field ApplicationID
    global CurrentApplicationAddress
	itxn_field Sender
    txn Assets 0
    itxn_field Assets
    load 2
    itob
    dup
    itxn_field ApplicationArgs
    itxn_field ApplicationArgs
	itxn_submit

	itxn TxID
    log
	itxn LastLog
	extract 4 0
    store 11
    load 11
    log
	
	byte 0x151f7c75
	load 9
	itob
	concat
	b branch_log_return

// Handles ABI method call for contract to contract update of a Proposal contract
method_proposal_update:

	// Checking Transaction group
    global GroupSize
    int 2
    ==
    assert
    // Checking the pay txn at index 0 of gtxn
    gtxn 0 RekeyTo
    global ZeroAddress
    ==
    assert
    gtxn 0 CloseRemainderTo
    global ZeroAddress
    ==
    assert
    gtxn 0 AssetCloseTo
    global ZeroAddress
    ==
    assert
    gtxn 0 TypeEnum
    int pay
    ==
    assert
    gtxn 0 Fee
    global MinTxnFee
    >=
    assert
    gtxn 0 Amount
    global MinTxnFee
    >=
    assert
    // Checking if the receiver of pay txn is the same as the current application address
    txn GroupIndex
    int 1
    -
    dup
    gtxns Receiver
    global CurrentApplicationAddress
    ==
    assert

    gtxns Amount
    global MinTxnFee
    int 2
    *
    >=
    assert
	
    // Process Transaction args
	// Proposal app id
    txn ApplicationArgs 2
	btoi
    store 0
    // Proposal manager address
    txn ApplicationArgs 3
    store 1
    // Proposal asset id
    txn ApplicationArgs 4
    store 2
    // Proposal title
    txn ApplicationArgs 5
    store 3
    // Proposal description
    txn ApplicationArgs 6
    store 4

    // Inner transaction to create the Proposal smart contract
    itxn_begin
    // itxn Type to "appl"
    int appl
    itxn_field TypeEnum
    // itxn OnCompletion
    int UpdateApplication
    itxn_field OnCompletion
	// itxn ApplicationID
    load 0
    itxn_field ApplicationID
    // itxn 
    txn Sender
    itxn_field Note
    method "update_proposal(application,uint64,uint64,address,address,string,string)address"
    itxn_field ApplicationArgs
    byte "proposal_app"
    box_get
    bz branch_error
    dup
    len
    int 2
    swap
    substring3
    itxn_field ApprovalProgram
    
    byte "proposal_clr"
    box_get
    bz branch_error
    dup
    len
    int 2
    swap
    substring3
    itxn_field ClearStateProgram
    global CurrentApplicationID
    itob
    itxn_field ApplicationArgs
    byte "gora_dao_asset_id"
    app_global_get
	itob
    itxn_field ApplicationArgs
	// Proposal asset id
    load 2
    itxn_field ApplicationArgs
	// Proposal manager address
    load 1
    itxn_field ApplicationArgs
    byte "gora_dao_manager_address"
	app_global_get
    itxn_field ApplicationArgs
	// Proposal title
    load 3
    itxn_field ApplicationArgs
	// Proposal description
    load 4
    itxn_field ApplicationArgs
    
    
    itxn_submit
    itxn TxID
    log
    itxn LastLog
	extract 4 0
    log


	byte 0x151f7c75
	load 0
	itob
	concat
	b branch_log_return

method_proposal_config:
	// Checking Transaction group
    global GroupSize
    int 5
    ==
    assert
    // Checking the pay txn at index 0 of gtxn
    gtxn 0 RekeyTo
    global ZeroAddress
    ==
    assert
    gtxn 0 CloseRemainderTo
    global ZeroAddress
    ==
    assert
    gtxn 0 AssetCloseTo
    global ZeroAddress
    ==
    assert
    gtxn 0 TypeEnum
    int pay
    ==
    assert
    gtxn 0 Fee
    global MinTxnFee
    >=
    assert
    gtxn 0 Amount
    global MinTxnFee
    >=
    assert

    // Checking if the receiver of pay txn is the same as the current application address
    txn GroupIndex
    int 1
    -
    dup
    gtxns Receiver
    global CurrentApplicationAddress
    ==
    assert

    gtxns Amount
    global MinTxnFee
    int 2
    *
    >=
    assert
	
    // Process Transaction args
	// Proposal app id
    txn ApplicationArgs 2
	btoi
    store 0
    // Proposal manager address
    txn ApplicationArgs 3
    store 1
    // Proposal asset id
    txn ApplicationArgs 4
    store 2


	byte 0x151f7c75
	global CurrentApplicationID
	itob
	concat
	b branch_log_return
branch_error:

	err

// Handles logging the last element on the stack then put a 1 on the top and return (log and approve)
branch_log_return:

	log

	int 1
	return

